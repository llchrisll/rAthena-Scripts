//===== rAthena Script =======================================
//= Arena Master
//===== By ===================================================
//= llchrisll
//===== Version ==============================================
//= 1.0 - Script Made
//		  Added Multi Kill & SoundEffects
//		  Added Bounty & Promotion Feature
//		  Fully tested & Rewritten most of the code
//		  - Since it didn't wanted to work like I wanted q-q
//		  - Couldn't test the Party PvP option though.
//		  Added a bonus at the bounty feature
//		  - If an player gains a "$@bty_bon_start" spree, "$@bty_bonus"
//			will be given to the player, who kill him/her.
//= 1.1 - Fixxed an Bounty bug - Thanks to raijin91
//          = Also the bounty bonus wasn't added, kinda forgot that it seems -.-.
//      - Changed the First Blood part
//      - Changed the Chat blocking part
//      - Shorted the sphree part a bit, still testing it.
//      - Added an Cost Multiplier for the shop to adjust the prices for Mid Rate and higher Servers.
//      - Added another function to get the map users in each room and each pvp.
//= 1.2 - Compatible with rAthena
//      - Fixed some bugs
//= 1.3 - Made the Sphree Announcer dynamically
//      - Made modification regarding the sphree ranking, removed doPromote; function
//      - Shorted the ranking display choices
//= 1.4 - Moved writeStats; into the kill part, bug with Logout prevented from saving in ranking
//      - Fixed an bug with "First Blood" announcer
//      - Updated the MvP Ranker to rAthena @getmonsterinfo has an check for MvP EXP now
//        - Removed limitation to MvP Arena, against every MvP useable now
//      - Fixxed Ranking bug, didn't save Kills and stuff correctly.
//      - Added an addtional Player Counter for each PvP Room
//      - Added Class Limitation regarding 3rd Jobs
//      - Fixxed an bug with Auto-Resurrect on Deathmatch Maps
//      - Fixxed another Ranking bug, didn't display correctly
//= 1.5 - Added Custom Bounty Map Feature, see line around '861'
//      - Added to the Bounty Hunter an Locater, which locates the player...
//      - Fixxed an bug about the Bounty Hunter, forgot to reset the @hunt variable
//      - Removed First Blood completely
//      - Removed Chat Block part completely + OnPCMapLoadEvent
//      - Fixxed an Arena Shop bug by using $@arena_pay = 2 (Permanent Price Array) - Thanks to Lelantos for the bug report
//      - Rewrote the whole check at the Party PvP, also fixxed the check for 
//        online party member counting > misplaced the variables for "isloggedin" ... - Thanks to Lelantos for the bug report
//===== Compatible With ======================================
//= Every rAthena Version [SQL]
//===== Tested With ==========================================
//= rAthena 17267 SQL Trunk 
//===== Description ==========================================
//= PvP / GvG Arenas with ranking ladder & MvP Ranker.
//= PvP Types:
//  - Normal
//  - Dispell: You'll be dispelled on Room entry.
//  - Deathmatch: You'll be auto resurrect when you die.
//    If you type @die, get comaed and similar, you'll be warped to your save point.
//  - Party PvP: 2vs2, 3vs3, 4vs4, 5vs5
//= PvP:
//  - There are 5 Rooms: Prontera, Alberta, Izlude, Payon, Morroc
//    PvP Types: Normal - Deathmatch has each one of these.
//    That means 5*3.
//  - Party PvP: There are 4 types and those also has one of room each.
//  > So 5*4.
//  -----------------------------------------------
//  = Total 35 Rooms !!!!
//  -----------------------------------------------
//= GvG: 5 Arenas, the default guild_vs1 - guild_vs5.
//===== Comments & Credits =============================================
// = Thanks to Lelantos for the idea of the Custom Bounty Map Feature
//=========== Item Usage Restrictions =======================
// Required File: db/pre-re/item_noequip.txt or db/re/item_noequip.txt
// Entries, insert these lines at the bottom of the file.
/*
// Disabled for PvP+GvG - PvP/GvG Arena [llchrisll]
607,3 // Yggdrasil Berry
608,3 // Yggdrasil Seed
*/
//===== MySQL Entries ========================================
/*
// v1.0
DROP TABLE IF EXISTS `arena_master`;
CREATE TABLE `arena_master` (
  `char_id` int(10) unsigned NOT NULL, 
  `kills` smallint(5) unsigned DEFAULT '0', 
  `death` smallint(5) unsigned DEFAULT '0', 
  `hscore` smallint(5) unsigned DEFAULT '0', 
  `bounty` smallint(5) unsigned DEFAULT '0',
  `bounty_pts` smallint(5) unsigned DEFAULT '0',
  `killer` smallint(5) unsigned DEFAULT '0', 
  `domi` smallint(5) unsigned DEFAULT '0', 
  `mega` smallint(5) unsigned DEFAULT '0', 
  `unstopp` smallint(5) unsigned DEFAULT '0', 
  `wicked` smallint(5) unsigned DEFAULT '0', 
  `monster` smallint(5) unsigned DEFAULT '0', 
  `godlike` smallint(5) unsigned DEFAULT '0', 
  `holys` smallint(5) unsigned DEFAULT '0', 
  `mvp` smallint(5) unsigned DEFAULT '0',
PRIMARY KEY (`char_id`)
) ENGINE=MyISAM;
*/
//=============== ToDo List ==========================
// = Maybe adding a even more detailed class limitation, but just maybe
//============================================================
// ================= Arena Master ================== //
prontera,142,168,5	script	Arena Master	808,{
set .@n$,"["+strnpcinfo(1)+"]";
function getPartyMapUsers;
function getPvPMapUsers;

mes .@n$;
mes "Hello, "+strcharinfo(0)+"!";
if(getgmlevel() > 79) 
	if(select("- GM Menu:- Player Menu") == 1) 
		goto GM_Menu;
if($@npc_load == 1) {
	mes "I'm sorry, but the Arena and it's features are not available yet. Try again in a few minutes please.";
	close;
}
mes "How can I help you?";
mes "Ranking Status: "+ ( ($@pvptable)?"Online":"Offline");
if(!$@pvptable) mes "Duo the ranking system being offline, no ranking will take place.";
next;
switch(select("- Go to Arenas:- See Rankings:- Visit Arena Shop:- Need Information:- Nevermind")) {
	
// Arenas
	case 1:
	set @sphr,0;
	set @bty,0;
	set @killct,0;
	mes .@n$;
	mes "In which arena do you want to fight?";
	next;
	switch(select("- PvP:- GvG:- Nevermind")) {
// PvP
		case 1:
		mes .@n$;
		mes "Now select the room you want to fight in.";
		set @map,select("- Prontera ["+getPvPMapUsers("All",1)+"]:- Alberta ["+getPvPMapUsers("All",2)+"]:- Izlude ["+getPvPMapUsers("All",3)+"]:- Payon ["+getPvPMapUsers("All",4)+"]:- Morroc ["+getPvPMapUsers("All",5)+"]");
		
		next;
		mes .@n$;
		mes "Please choose the PvP Type:";
		mes "This arena can hold up to "+$@muser[@map]+" player(s).";
		mes "Party PvP is different of course.";
		mes "In the \"()\" stands, if 3rd Jobs, only 3rd Jobs or All Jobs are allowed to enter.";
		next;
		switch($@class_disp[@map]) {
			case 0: set .@m_pvpdisp$,"No 3rd Jobs"; break;
			case 1: set .@m_pvpdisp$,"Only 3rd Jobs"; break;
			case 2: set .@m_pvpdisp$,"All Jobs"; break;
		}
		switch($@class_n[@map]) {
			case 0: set .@m_pvpn$,"No 3rd Jobs"; break;
			case 1: set .@m_pvpn$,"Only 3rd Jobs"; break;
			case 2: set .@m_pvpn$,"All Jobs"; break;
		}
		switch($@class_dm[@map]) {
			case 0: set .@m_pvpdm$,"No 3rd Jobs"; break;
			case 1: set .@m_pvpdm$,"Only 3rd Jobs"; break;
			case 2: set .@m_pvpdm$,"All Jobs"; break;
		}
		switch(select("- PvP (Dispell) ["+getPvPMapUsers("disp",@map)+"] ("+.@m_pvpdisp$+"):- Normal PvP ["+getPvPMapUsers("n",@map)+"] ("+.@m_pvpn$+"):- PvP Deathmatch["+getPvPMapUsers("dm",@map)+"] ("+.@m_pvpdm$+"):- Party PvP["+getPartyMapUsers(6)+"]")) {
		// PvP Dispell
			case 1:
			mes .@n$;
			if($@class_disp[@map] == 0 && eaclass()&EAJL_THIRD) {
				mes "I'm sorry, but 3rd Classes are not allowed in this PvP Room.";
				close;
			} else if($@class_disp[@map] == 1 && !(eaclass()&EAJL_THIRD)) {
				mes "I'm sorry, but only 3rd Classes are allowed in this PvP Room.";
				close;
			}
			if(getmapusers($@disppvp$[@map]) >= $@muser[@map]) {
				mes "I'm sorry, but this room is already full.";
				close;
			}
			mes "I'll warp you now to the "+ $@room$[@map]+" PvP Room, also remove all your buffs.";
			close2;
			unitskilluseid getcharid(3),"SA_DISPELL",5;
			warp $@disppvp$[@map],0,0;
			end;
			
		//PvP Normal
			case 2:
			mes .@n$;
			if($@class_n[@map] == 0 && eaclass()&EAJL_THIRD) {
				mes "I'm sorry, but 3rd Classes are not allowed in this PvP Room.";
				close;
			} else if($@class_n[@map] == 1 && !(eaclass()&EAJL_THIRD)) {
				mes "I'm sorry, but only 3rd Classes are allowed in this PvP Room.";
				close;
			}
			if(getmapusers($@npvp$[@map]) >= $@muser[@map]) {
				mes "I'm sorry, but this room is already full.";
				close;
			}
			mes "I'll warp you now to the "+ $@room$[@map]+" PvP Room.";
			close2;
			warp $@npvp$[@map],0,0;
			end;
			
		// PvP DM	
			case 3:
			mes .@n$;
			if($@class_dm[@map] == 0 && eaclass()&EAJL_THIRD) {
				mes "I'm sorry, but 3rd Classes are not allowed in this PvP Room.";
				close;
			} else if($@class_dm[@map] == 1 && !(eaclass()&EAJL_THIRD)) {
				mes "I'm sorry, but only 3rd Classes are allowed in this PvP Room.";
				close;
			}
			if(getmapusers($@dmpvp$[@map]) >= $@muser[@map]) {
				mes "I'm sorry, but this room is already full.";
				close;
			}
			mes "I'll warp you now to the "+ $@room$[@map]+" PvP Deathmatch Room.";
			close2;
			if(getmapusers($@dmpvp$[@map]) == 0) 
				set $@first,0;
			set @dm,1;
			warp $@dmpvp$[@map],0,0;
			end;
		
		// Party PvP
			case 4:
			mes .@n$;
			if(!getcharid(1)) {
				mes "I'm sorry, but you are not in a party.";
				mes "Please come back as soon you have found one.";
				close;
			}
			if(getpartyleader(getcharid(1)) != strcharinfo(0)) {
				mes "I'm sorry, but only your leader can register your party.";
				close;
			}
			set @pty_id,getcharid(1);
			getpartymember(@pty_id,1);
			getpartymember(@pty_id,2);
			set @ptymem,$@partymembercount;
			copyarray @ptymemid[0],$@partymembercid,@ptymem; // Char ID
			copyarray @ptymemaid[0],$@partymemberaid,@ptymem; // Account ID
			set .@pt_ct,0;
			for ( set .@pm,0; .@pm < @ptymem; set .@pm,.@pm + 1) 
				if( isloggedin(@ptymemaid[.@pm],@ptymemid[.@pm]) ) 
					set .@pt_ct,.@pt_ct + 1;

			if(.@pt_ct < 2) {
				mes "It seems like you don't the required members for the Party vs Party Mode.";
				mes "You need at least two players, which have to be online, to be able to participate.";
				close;
			}
			set .@pt,.@pt_ct;
			mes "Chosen Party vs Party Mode:";
			mes .@pt+"vs"+.@pt+" Party PvP";
			mes "Class Restriction:";
			switch( getd("$@class_p"+.@pt+"["+@map+"]") ) {
				case 0: mes "No 3rd Jobs allowed"; break;
				case 1: mes "Only 3rd Jobs allowed"; break;
				case 2: mes "All Jobs allowed"; break;
			}
			next;
			mes .@n$;
			if(getPartyMapUsers(.@pt,@map) >= (.@pt*.@pt)) {
				mes "I'm sorry, but this "+.@pt+"vs"+.@pt+" Arena is already full.";
				mes "Please try another.";
				close;
			}
			getpartymember(@pty_id,1);
			getpartymember(@pty_id,2);
			set @ptymem,$@partymembercount;
			copyarray @ptymemid[0],$@partymembercid,@ptymem; // Char ID
			copyarray @ptymemaid[0],$@partymemberaid,@ptymem; // Account ID
			for ( set .@pm,0; .@pm < @ptymem; set .@pm,.@pm + 1) 
				if( isloggedin(@ptymemaid[.@pm],@ptymemid[.@pm]) ) 
					if(getd("$@class_p"+.@pt+"["+@map+"]") == 0 && eaclass()&EAJL_THIRD) {
						mes "I'm sorry, but 3rd Classes are not allowed in this PvP Room.";
						close;
					} else if(getd("$@class_p"+.@pt+"["+@map+"]") == 1 && !(eaclass()&EAJL_THIRD)) {
						mes "I'm sorry, but only 3rd Classes are allowed in this PvP Room.";
						close;
					}
			mes "I'll warp your party now.";
			mes "Good luck.";
			close2;
			warpparty getd("$@pt"+.@pt+"pvp$["+@map+"]"),0,0,@pty_id;
			end;
		}
		break;

function	getPvPMapUsers	{
	// getarg(0) - PvP Type:
	// - disp: Dispell
	// - n: Normal
	// - dm: Deathmatch
	// - All: Room Counter 
	// getarg(1) == Room
	if(getarg(0) != "All") 
	 	return getmapusers(getd("$@"+getarg(0)+"pvp$["+getarg(1)+"]"));
	 else 
		 return getmapusers($@disppvp$[getarg(1)]) + getmapusers($@npvp$[getarg(1)]) + getmapusers($@dmpvp$[getarg(1)]);
}
function	getPartyMapUsers	{
	// getarg(0) == Party PvP Type (2vs2,3vs3, etc...) > 2,3, etc...
	//              if it's a 6 = Total Map Users of the Party PvP Maps
	// getarg(1) == Room
	if(getarg(0) == 6) {
		set @pt2play,getmapusers($@pt2pvp$[1]) + getmapusers($@pt2pvp$[2]) + getmapusers($@pt2pvp$[3]) + getmapusers($@pt2pvp$[4]) + getmapusers($@pt2pvp$[5]);
		set @pt3play,getmapusers($@pt3pvp$[1]) + getmapusers($@pt3pvp$[2]) + getmapusers($@pt3pvp$[3]) + getmapusers($@pt3pvp$[4]) + getmapusers($@pt3pvp$[5]);
		set @pt4play,getmapusers($@pt4pvp$[1]) + getmapusers($@pt4pvp$[2]) + getmapusers($@pt4pvp$[3]) + getmapusers($@pt4pvp$[4]) + getmapusers($@pt4pvp$[5]);
		set @pt5play,getmapusers($@pt5pvp$[1]) + getmapusers($@pt5pvp$[2]) + getmapusers($@pt5pvp$[3]) + getmapusers($@pt5pvp$[4]) + getmapusers($@pt5pvp$[5]);
		return @pt2play + @pt3play + @pt4play + @pt5play;
		
	} else return getmapusers(getd("$@pt"+getarg(0)+"pvp$["+getarg(1)+"]"));
}
		
	// GvG
		case 2:
		mes .@n$;
		if(!getcharid(2)) {
			mes "I'm sorry, but you are not in a guild.";
			close;
		}
		mes "Please choose the room you want to join:";
		mes "Each arena can hold up to "+$@mgvg+" player(s).";
		if($@same_gld) {
			mes "Same Guild Requirement is active:";
			mes "If guilds already are in this room, you have to be a member of one of them so that you can join.";
			mes "Also an maximum of "+$@mpgld+" members per guild can be in one room.";
		}
		next;
		set .@rchoi,select("- GvG 1 ["+getmapusers($@gvgr$[0])+"]:- GvG 2 ["+getmapusers($@gvgr$[1])+"]:- GvG 3 ["+getmapusers($@gvgr$[2])+"]:- GvG 4 ["+getmapusers($@gvgr$[3])+"]:- GvG 5 ["+getmapusers($@gvgr$[4])+"]") - 1;
		mes .@n$;
		if($@same_gld) {
			set gvgc,@menu;
			if(getd("$@gvg_"+gvgc+"_gld_1") == 0) {
				mes "I will warp you now.";
				close2;
				warp $@gvgr$[.@rchoi],$@gvg_gld1_x[.@rchoi],$@gvg_gld1_y[.@rchoi];
				setd("$@gvg_"+gvgc+"_gld_1"),getcharid(2);
				setd("$@gvg_"+gvgc+"_1_ppl"),getd("$@gvg_"+gvgc+"_1_ppl") + 1;
				end;
			} else if(getd("$@gvg_"+gvgc+"_gld_2") == 0) {
				mes "I will warp you now.";
				close2;
				warp $@gvgr$[.@rchoi],$@gvg_gld2_x[.@rchoi],$@gvg_gld2_x[.@rchoi];
				setd("$@gvg_"+gvgc+"_gld_2"),getcharid(2);
				setd("$@gvg_"+gvgc+"_2_ppl"),getd("$@gvg_"+gvgc+"_2_ppl") + 1;
				end;
			}
			if(getd("$@gvg_"+gvgc+"_gld_1") == getcharid(2)) {
				if(getd("$@gvg_"+gvgc+"_1_ppl") >= $@mpgld) {
					mes "I'm sorry, but your guild's side is already full.";
					close;
				}
				mes "I will warp you now.";
				close2;
				set @rand,rand(1,2);
				warp $@gvgr$[.@rchoi],getd("$@gvg_gld1_x"+@rand+"["+.@rchoi+"]"),getd("$@gvg_gld1_y"+@rand+"["+.@rchoi+"]");
				setd("$@gvg_"+gvgc+"_1_ppl"),getd("$@gvg_"+gvgc+"_1_ppl") + 1;
				end;
			} else if(getd("$@gvg_"+gvgc+"_gld_2") == getcharid(2)) {
				if(getd("$@gvg_"+gvgc+"_2_ppl") >= $@mpgld) {
					mes "I'm sorry, but your guild's side is already full.";
					close;
				}
				mes "I will warp you now.";
				close2;
				set @rand,rand(1,2);
				warp $@gvgr$[.@rchoi],getd("$@gvg_gld2_x"+@rand+"["+.@rchoi+"]"),getd("$@gvg_gld2_y"+@rand+"["+.@rchoi+"]");
				setd("$@gvg_"+gvgc+"_2_ppl"),getd("$@gvg_"+gvgc+"_2_ppl") + 1;
				end;
			} else {
				mes "I'm sorry, but you can't enter this room since it's in use of other guilds already.";
				close;
			}
		} else {
			if(getmapusers($@gvgr$[.@rchoi]) >= $@mgvg) {
				mes "I'm sorry, but this Arena is already full.";
				close;
			}
			mes "I will warp you now.";
			close2;
			warp $@gvgr$[.@rchoi],0,0;
			end;
		}
		break;
		
	// Nevermind
		case 3:
		goto R_Bye;
		break;
	}

// Ranking
	case 2:
	mes .@n$;
	if(!$@pvptable) {
		mes "I'm sorry, the ranking system is offline and thus I can't show you any ranking.";
		close;
	}
	mes "Which ranking do you wanna see?";
	next;
	
	set @rank,select("- Kills:- Deaths:- High Scores:- Killing Sphree:- Dominating:- Mega Kill:- Unstoppable:- Wicked Sick:- Monster Kill:- GodLike:- Holy Sh1t:- MvP");
	if(@rank > 3 && @rank < 12) // PvP Sphree Rankers
		set .@listr$,$@sphree_rank$[@rank-4];
	else if(@rank == 12) // MvP Hunters
		set .@listr$,"mvp";
	else { // PvP General K/D/High Score Rankers
		setarray .@db_rank$[0],"kills","death","hscore";
		set .@listr$,.@db_rank$[@rank-1];
	}
	mes .@n$;
	mes "Which list do you want to see?";
	next;
	set .@limit,select("- Top 5:- Top 10:- Top 15:- Top 20:- Top 25:- Top 30:- Nevermind") * 5;
	if(.@limit == 35) 
		goto R_Bye;
		
	mes .@n$;
	mes "Ranking:";
	switch(@rank) {

		case 1:	mes set(.@rankn$,"Top "+.@limit+" Killers");	set .@rankt$,"Kills";	break;
		case 2:	mes set(.@rankn$,"Top "+.@limit+" Victims");	set .@rankt$,"Deaths";	break;
		case 3:	mes set(.@rankn$,"Top "+.@limit+" High Scores");	set .@rankt$,"High Score";	break;
		case 12: mes set(.@rankn$,"Top "+.@limit+" MvP Hunters");	set .@rankt$,"MvP Kills";	break;
		default:	mes set(.@rankn$,"Top "+.@limit+" "+$@sphree_names$[@rank-4]+" Rankers");	set .@rankt$,$@sphree_names$[@rank-4]+"s";	break;
	}
	mes "Correct?";
	next;
	if(select("- Yes:- No") == 2) goto R_Bye;
	mes .@n$;
	mes "I will now list the ranking in your Chat Box.";
	close2;
	dispbottom "======= "+.@rankn$+" ===========";
	dispbottom "Player Name:    "+.@rankt$;
	query_sql "SELECT `char_id` , `"+.@listr$+"` FROM `arena_master` WHERE `"+.@listr$+"` > 0 ORDER BY `"+.@listr$+"` DESC LIMIT "+.@limit,.@ch_id,.@sh_r;
	if(!.@sh_r[0]) {
		dispbottom "No Player found.";
		end;
	}
	for( set .@r,0; .@r < getarraysize(.@sh_r); set .@r,.@r + 1) {
		query_sql "SELECT `name` FROM `char` WHERE `char_id` = '"+.@ch_id[.@r]+"'",.@ch_na$;
		dispbottom .@ch_na$+":    "+.@sh_r[.@r];
	}
	deletearray .@ch_id[0],128;
	deletearray .@sh_r[0],128;
	end;
	
// Arena Shop
	case 3:
	mes .@n$;
	mes "Hello, I can sell your useful items for the daily PvP need and more.";
	mes "Depending on the bounty, I will open the respective shop.";
	next;
	mes .@n$;
	set @pts,0;
	switch($@bty_type) {
		
		case 0:
		mes "I'm sorry, but the bounty feature is not enabled.";
		close;
		
		case 1:
		mes "I will now open the Bounty Shop, which uses \"Zeny\" as payment.";
		break;
		
		case 2:
		mes "I will now open the Bounty Shop, which uses \"Cash Points\" as payment.";
		mes "You have "+#CASHPOINTS+" Cash Points.";
		break;
		
		case 3:
		mes "I will now open the Bounty Shop, which uses \"Bounty Points\" as payment.";
		query_sql "SELECT `bounty_pts` FROM `arena_master` WHERE `char_id` = '"+getcharid(0)+"'",@pts;
		mes "You have "+@pts+" Bounty Points.";
		break;
	}
	close2;
	callshop "Arena_Shop",1;
	npcshopattach "Arena_Shop";
	end;

	OnBuyItem:
	for( set @s,0; @s < getarraysize(@bought_nameid); set @s,@s + 1) {
		for( set @i,0; @i < getarraysize($@arena_sh); set @i,@i + 1) {
			if(@bought_nameid[@s] == $@arena_sh[@i]) {
				if(checkweight(@bought_nameid[@s],@bought_quantity[@s]) == 1) {
					set @c_item,(@bought_quantity[@s]*$@arena_ar[@i]);
					set @total,(@total + @c_item);
					break;
				} else {
					dispbottom "Arena Shop: You have reached your weight limit, lose some weight and come back please.";
					end;
				}
			}
		}
	}
	switch($@bty_type) {

		case 1:
		if(Zeny >= @total) {
			for ( set @b,0; @b < getarraysize(@bought_nameid); set @b,@b + 1) 
				getitem @bought_nameid[@b],@bought_quantity[@b];
				
			set Zeny,Zeny - @total;

		} else dispbottom "You don't have enough Zeny.";
		break;
		
		case 2: 
		if(#CASHPOINTS >= @total) {
			for ( set @b,0; @b < getarraysize(@bought_nameid); set @b,@b + 1) 
				getitem @bought_nameid[@b],@bought_quantity[@b];

			set #CASHPOINTS,#CASHPOINTS - @total;

		} else dispbottom "You don't have enough Cash Points, currently "+#CASHPOINTS;
		break;
		
		case 3:
		if(@pts >= @total) {
			for ( set @b,0; @b < getarraysize(@bought_nameid); set @b,@b + 1) 
				getitem @bought_nameid[@b],@bought_quantity[@b];

			query_sql "UPDATE `arena_master` SET `bounty_pts` = `bounty_pts` - "+@total+" WHERE `char_id` = '"+getcharid(0)+"'";

		} else dispbottom "You don't have enough Bounty Points, currently "+@pts;
		break;
	}
	set @total,0;
	deletearray @bought_nameid[0],128;
	deletearray @bought_quantity[0],128;
	end;
	
// Information
	case 4:
	mes .@n$;
	mes "What do you want to know about?";
	next;
	switch(select("- PvP:- GvG:- Sphree:- Ranking:- Bounty Feature:- Nevermind")) {
		
		case 1:
		mes "[=== PvP ===]";
		mes "Each PvP Type has five rooms, displayed as";
		mes "Prontera, Alberta, Izlude, Payon and Morroc.";
		mes "Party PvP has 4 types and each type has also five rooms.";
		next;
		mes "[=== PvP ===]";
		mes "PvP Types:";
		mes " - Normal";
		mes " - Dispell";
		mes " - Deathmatch";
		mes " - Party PvP:";
		mes "   - 2vs2";
		mes "   - 3vs3";
		mes "   - 4vs4";
		mes "   - 5vs5";
		next;
		mes "[=== PvP ===]";
		mes "Dispell PvP:";
		mes " - You'll be dispelled, when";
		mes "	you entry an room.";
		mes " ";
		mes "Deathmatch PvP:";
		mes " - You'll be auto-resurrect on";
		mes "   death, except you type @die,";
		mes "	get comaed or similar, then";
		mes "   you'll be warped to your savepoint.";
		close;
		
		case 2:
		mes "[=== GvG ===]";
		if($@same_gld) {
			mes "There can be two guilds in one room at the same time with maximum "+$@mpgld+" members per guild.";
			mes "Each guild has two spawn postions, Guild 1 has North and West and Guild 2 has East and South,";
			mes "which will be chosen randomly on room entry, also only members";
			mes "of 'em can enter the same room until no one is left of the respective guild.";
		} else mes "Only members of a guild can enter a room and the maximum players, which can enter are "+$@mgvg+".";
		close;
		
		case 3:
		mes "[=== Sphree ===]";
		mes "Sphree - Required Kills.";
		mes "First Blood - 1";
		for ( set .@s,0; .@s < getarraysize($@sphree_names$); set .@s,.@s + 1) 
			mes $@sphree_names$[.@s] +" - "+$@sphree_kills[.@s];
		
		next;
		mes "[=== Sphree ===]";
		mes "For each sphree you accomplish, except First Blood, you gain an point for the bounty feature, if it has been activated.";
		mes "For more ask me about the Bounty Feature.";
		next;
		mes "[=== Sphree ===]";
		mes "Also there is the Multi Kill Sphrees, but you won't recieve points for them.";
		mes "The time span you can achieve them is "+ $@multi/1000+" seconds.";
		next;
		mes "[=== Sphree ===]";
		mes "In this time you, if you kill";
		mes " - one more player = Double Kill";
		mes " - two more players = Triple Kill";
		mes " - three or more players = Ultra Kill";
		close;
		
		case 4:
		mes "[=== Ranking ===]";
		mes "Kills, Deaths, Sphree Counts (like Killing Sphree), the highest kill count till you died and lists it in either a Top 5, 10, 15, 20, 25 or	a Top 30 List with the wanted information you have chosen, will be displayed.";
		close;
		
		case 5:
		mes .@n$;
		if(!$@bty_type) {
			mes "This feature isn't activate.";
			close;
		}
		mes "There are three different bounty rewards, which are the following:";
		mes " - Zeny";
		mes " - Cash Points";
		mes " - Bounty Points";
		next;
		mes .@n$;
		mes "But depending on the setting which has been made by the GM Team, only one will be used.";
		mes "Also for each of these an Shop exists and only one shop can be accessed.";
		mes "Current Bounty Reward: "+ $@bty_price + " " + ( ($@bty_type == 1)?"Zeny": ( ($@bty_type == 2)?"Cash Points":"Bounty Points") );
		mes "Next is an Bounty Bonus, which automatically activates when someone reaches "+$@bty_bon_start+" kills.";
		mes "An announce will be displayed, to stop him and that you get an bonus to the bounty price, which depends on the bounty type.";
		mes "Current setting is: "+$@bty_bonus+" " + ( ($@bty_type == 1)?"Zeny": ( ($@bty_type == 2)?"Cash Points":"Bounty Points") );
		mes "Also there is an Bounty Bonus Multiplier, which can be turned on and off by the GM Team as needed.";
		mes "The reason for that is, that the Zeny Bounty Bonus was 5 Zeny (default settings), which is kinda lame ;I.";
		mes "But the value of that will be an secret :I.";
		next;
		mes .@n$;
		mes "The bounty depends on your victims sphree.";
		mes "Here is a list of them:";
		mes " ";
		mes "Sphree - Points";
		for ( set .@s,0; .@s < getarraysize($@sphree_names$); set .@s,.@s + 1) 
			mes $@sphree_names$[.@s] +" - "+.@s;
			
		next;
		
		case 6:
		break;
	}

// Nevermind
	case 5:
	break;
}

R_Bye:
mes .@n$;
mes "Okay, see ya.";
close;

// GM Menu
GM_Menu:
mes "How can I help you?";
next;
switch(select("- Tables Settings:- Debug State:- Nevermind")) {
	
	case 1:
	mes .@n$;
	mes "Tables Status: "+( ($@pvptable == 1)?"":"Not ")+"Installed";
	mes "What do you want to do?";
	next;
	if($@pvptable == 1) {
		switch(select("- Erase Content from Table:- Delete Tables completely:- Nevermind")) {
			case 1:
			mes .@n$;
			mes "Are you sure, that you want to delete the rankings from the tables?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .@n$;
			mes "Here we go!";
			query_sql "TRUNCATE TABLE `arena_master`";
			break;
			
			case 2:
			mes .@n$;
			mes "Are you sure, that you want to delete the whole table from the database?";
			if(select("- Yes:- No") - 1) close;
			next;
			mes .@n$;
			mes "Here we go!!!";
			query_sql "DROP TABLE IF EXISTS `arena_master`";
			break;
			
			case 3:
			break;
		}
	} else if($@pvptable == 0) {
		if(select("- Create Tables:- Nevermind") - 1) close;
		mes .@n$;
		mes "Are you sure, that you want to create the tables for the rankings?";
		if(select("- Yes:- No") - 1) close;
		next;
		mes .@n$;
		mes "Here we go!";
		query_sql "CREATE TABLE `arena_master` ( `char_id` int(10) unsigned NOT NULL, `kills` smallint(5) unsigned DEFAULT '0', `death` smallint(5) unsigned DEFAULT '0', `hscore` smallint(5) unsigned DEFAULT '0', `bounty` smallint(5) unsigned DEFAULT '0', `bounty_pts` smallint(5) unsigned DEFAULT '0', `killer` smallint(5) unsigned DEFAULT '0', `domi` smallint(5) unsigned DEFAULT '0', `mega` smallint(5) unsigned DEFAULT '0', `unstopp` smallint(5) unsigned DEFAULT '0', `wicked` smallint(5) unsigned DEFAULT '0', `monster` smallint(5) unsigned DEFAULT '0', `godlike` smallint(5) unsigned DEFAULT '0', `holys` smallint(5) unsigned DEFAULT '0', `mvp` smallint(5) unsigned DEFAULT '0' )";
		break;
	}
	close;
	
	case 2:
	mes .@n$;
	mes "Debug State: "+( ($@debug == 1)?"Online":"Offline");
	mes "Do you want to change it?";
	if(select("- Yes:- No") - 1) close;
	next;
	mes .@n$;
	mes "Debug State has been turned "+ ( (!$@debug)?"on":"off")+".";
	set $@debug,!$@debug;
	break;
	
	case 3:
	break;
}
close;

OnStatus:
while($@npc_load) {
	delwaitingroom;
	waitingroom "Arena: Closed",0;
	sleep 5000;
}
delwaitingroom;
waitingroom "NPC Loading Timer: "+(getvariableofnpc(.time,"PvPGvGMain")/1000)+" seconds.",0;
sleep 5000;
delwaitingroom;
announce "PvP/GvG Arena is now opened.",0;
end;
}
// =============== Bounty Shop =========== //
-	shop	Arena_Shop	-1,512:-1
// =============== Main Settings ===============//
-	script	PvPGvGMain	-1,{
end;

OnInit:
freeloop(1);
// =============== Ranking Check & Debug Messages ================//
set $@npc_load,1;
initnpctimer;
donpcevent "Arena Master::OnStatus";
if($@debug) debugmes "====== Arena Master - Debug initiate... ========";
if(query_sql("SHOW TABLES LIKE 'arena_master'",.@pvptab[0]) == 0) set $@pvptable,0; else set $@pvptable,1;
// ============== Sphree Settings ============//
setarray $@sphree_names$[0],"Killing Sphree","Dominating","Mega Kill","Unstoppable","Wicked Sick","Monster Kill","GodLike","Holy Sh1t";
setarray $@sphree_kills[0],3,5,7,9,11,13,15,17; // Kills required for each sphree.
setarray $@sphree_wav$[0],"killingspree","dominating","megakill","unstoppable","wickedsick","monsterkill","godlike","holyshit"; // WAV file names
setarray $@sphree_rank$[0],"killer","domi","mega","unstopp","wicked","monster","godlike","holys"; // Variable Names for the ranking of each sphree
set $@multi,5000; // Time Span for chance to gain Multi-Kill in milliseconds.
if($@debug) debugmes "PvP Sphree Names loaded: "+getarraysize($@sphree_names$);
if($@debug) debugmes "PvP Sphree Kills loaded: "+getarraysize($@sphree_kills);
if($@debug) debugmes "PvP Sphree WAV Names loaded: "+getarraysize($@sphree_wav$);
if($@debug) debugmes "PvP Sphree Ranking Names loaded: "+getarraysize($@sphree_rank$);
// ============== Bounty Settings =========//
// Bounty Type: 
// * 0 = Off
// * 1 = Zeny
// * 2 = Cash Points
// * 3 = Bounty Points
set $@bty_type,1;
if($@bty_type) {
	set $@bty_bonus,5; // Bonus, if you kill an player
	set $@bty_bon_start,4; //which has achieved an spree of "$@bty_bon_start"
	set $@bty_bonus_multi,1; // Use Bonus Multiplier depending on the Bounty Type? See seperate sections below

// ====== Clearing Array on Whisper to prevent double entries ========
	if(.@re_in) {
		// Resetting Shop
		npcshopitem "Arena_Shop",512,10;
		// Deleting Prices
		deletearray $@arena_ar[0],128; 
	}

	// Shop Items: PvP useful stuff D:, like Elements, Boxes and Food
	// These will be used for all shops, but the prices depends on the Bounty Type you have set.
	setarray $@arena_sh[0],12020,12028,12029,12030,12031,12032,12033,12034,12108,12215,12216,12217,12218,12219,12075,12080,12085,12090,12095,12100,12114,12115,12116,12117,12118,12119,12120,12121,13543,13544,13545,13546,13547,14593;
	npcshopdelitem "Arena_Shop",512;
	// Prices:
	// Two ways of doing so:
	// - Per formula
	// - Per Array
	// You can determine for each currency the way of calculation and prices.
	// If you want to use the formula, set ".arena_pay" to 1.
	// If you wanna use the array, set ".arena_pay" to 2, 
	// but remember to put the prices for all items.
	set $@arena_pay,2;
	
	// Cost Multiplier:
	// The value here will multiply the costs in the shop.
	// * Recommended Values:
	// - Low Rate: 1
	// - Mid Rate: 50
	// - High Rate: 100
	// Note: On Mid Rate and High Rate Servers, it's easy to gain money very fast.
	// So I added this to only increase the costs in the shop, but not the gain per kill.
	// Note2: NEVER PUT '0', if you know calculation/mathematic then you should know
	// what happens if you multiply with '0' :I.
	set $@arena_multi,50; 

	// =============== Zeny ========== //
	if($@bty_type == 1) 
		set $@bty_price,5000; // Zeny per Sphree
	
	// =============== Cash Points ========== //
	else if($@bty_type == 2)
		set $@bty_price,2; // Cash Points per Sphree
	
	// =============== Bounty Points ========== //
	else if($@bty_type == 3)
		set $@bty_price,5; // Bounty Points per Sphree

	set $@bty_bonus_m,1000; // Multiplier for Bounty Bonus
	

	if($@arena_pay == 1) {
// ============== * By Formula * ================
// Note: If the formula results in a decimal number it will be degraded.

		for ( set .@s,0; .@s < getarraysize($@arena_sh); set .@s,.@s + 1) {
		// =============== Zeny Formula ========== //
			// Default: (($@bty_price+(.@s*($@bty_price/10)))/2)*$@arena_multi
			// Base Price = $@bty_price, "Multiplier" = + (Index ID* ($@bty_price/10))/2
			// Example: 5000 + (4(=Index of 12031)*(5000/10=500) > 7000/2 = 3500 Zeny for 12031
			// With Multiplier: "$@arena_multi = 50": 3500 * 50 = 175000
			if($@bty_type == 1) set .@arena_fo,(($@bty_price+(.@s*($@bty_price/10)))/2)*$@arena_multi;
			
		// =============== Cash Points Formula ========== //
			// Default: (($@bty_price+.@s)/2)*$@arena_multi
			// Base Price = $@bty_price, "Multiplier" = + Index ID/2
			// Example: 2 + 4(=Index of 12031) > 6/2 = 3 Cash Points for 12031
			// With Multiplier: "$@arena_multi = 50": 3 * 50 = 150
			else if($@bty_type == 2) set .@arena_fo,(($@bty_price+.@s)/2)*$@arena_multi;
			
		// =============== Bounty Points Formula ========== //
			// Default: (($@bty_price+(.@s*10))/2)*$@arena_multi
			// Base Price = $@bty_price, "Multiplier" = + (Index ID* 10)/2
			// Example: 5 + (4(=Index of 12031)*10) > 45/2 = 22 Bounty Points for 12031
			// With Multiplier: "$@arena_multi = 50": 22 * 50 = 1100
			else if($@bty_type == 3) set .@arena_fo,(($@bty_price+(.@s*10))/2)*$@arena_multi;
			
		// =============== Adding the price (calculated by the formula) to the array ========== //
			setarray $@arena_ar[.@s],.@arena_fo;
		}

	// Price Array
	} else
		setarray $@arena_ar[0],100; // Etc
	
	// Adding Items and Prices to the Shop
	for ( set .@s,0; .@s < getarraysize($@arena_sh); set .@s,.@s + 1)
		npcshopadditem "Arena_Shop",$@arena_sh[.@s],$@arena_ar[.@s];
		
	// Custom Bounty Maps
	// = Maps on which you also can gain the reward
	// 1 = On
	// 0 = Off, ordinary ones
	set $@bty_cmap,1;
	
	if($@bty_cmap == 1) {
		// Map Type:
		// 1 = Every Map with PvP/GvG Mapflags
		// 2 = Specific (Array below)
		set $@bty_cmap_type,2;
		
		// Map Array
		// Put the either the whole map name
		// or a part of it to add more maps with the part containing
		setarray $@bty_cmaps$[0],"prt_fild08","prt_gld";
		
		// Which map is a PvP/GvG Map?
		// 6 = PvP
		// 9 = GvG
		// Note: In the same order as above
		setarray $@bty_cmap_pg[0],6,9;
			
		// Mapflags
		setarray $@bty_cmapfl[0],0, //mf_nomemo
							//	 1, //mf_noteleport
								 2, //mf_nosave
								 3, //mf_nobranch
								 4, //mf_nopenalty
								 5, //mf_nozenypenalty
								11, //mf_notrade
							//	13, //mf_nowarp
							//	22, //mf_nogo
								33, //mf_noreturn
							//	34, //mf_nowarpto
							//	35, //mf_nightmaredrop
								38, //mf_nodrop
								41, //mf_novending
								44, //mf_noexppenalty
								14, //mf_partylock
								 7; //mf_pvp_noparty

		// ======= Auto Mapflag Adding ===============//
		for ( set .@m,0; .@m < getarraysize($@bty_cmaps$); set .@m,.@m + 1 ) {
			if($@debug) debugmes "Mapflag set at "+$@bty_cmaps$[.@m] + " initiate...";
			// Use this if you disallows parties
			//	for ( set .@mf,0; .@mf < getarraysize($@bty_cmapfl); set .@mf,.@mf + 1 ) {

			// Use this if you allow parties
				for ( set .@mf,0; .@mf < (getarraysize($@bty_cmapfl) - 2); set .@mf,.@mf + 1 ) {
					setmapflag $@bty_cmaps$[.@m],$@bty_cmapfl[.@mf];
					if($@debug) debugmes "Mapflag "+ $@bty_cmapfl[.@mf] +" set at "+$@bty_cmaps$[.@m] + " complete";
				}
				// PvP or GvG Mapflag
				setmapflag $@bty_cmaps$[.@m],$@bty_cmap_pg[.@m];
				if($@debug) debugmes "Mapflag "+ $@bty_cmap_pg[.@m] +" set at "+$@bty_cmaps$[.@m] + " complete";
		}
	}
}
if($@debug) debugmes "Bounty Shop - Currency: " + ( ($@bty_type == 1)?"Zeny": ( ($@bty_type == 2)?"Cash Points":"Bounty Points") ) + ", Basic Price: "+ $@bty_price + ",  Items: "+getarraysize($@arena_sh)+", Prices: "+getarraysize($@arena_ar);
// Bounty Calculation:
// Scroll down till the line with "function	getBounty	{", should be near Line 813
if($@debug) debugmes "Bounty Settings loaded: "+$@bty_type;
// ============ GvG Settings ============//
set $@mpgld,20; //Max Guild Members per Guild 
if($@debug) debugmes "Max Members per Guild loaded: "+$@mpgld;
set $@mgvg,$@mpgld*2; // Total Members per Arena.
if($@debug) debugmes "Max Members per Arena loaded: "+$@mgvg;
set $@same_gld,1; // Same Guild requirement: 1 = On / 0 = Off
if($@debug) debugmes "Same Guild Requirement loaded: "+$@same_gld;
setarray $@gvgr$[0],"guild_vs1","guild_vs2","guild_vs3","guild_vs4","guild_vs5";
if($@debug) debugmes "Guild Maps loaded: "+$@gvgr$[0];
// ======== Mapflags ======== //
// If you want to add more mapflags, 
// add them before the last two, which are for allowing party or not
setarray $@gvg_mf[0],0, //mf_nomemo
					 1, //mf_noteleport
					 2, //mf_nosave
					 3, //mf_nobranch
					 4, //mf_nopenalty
					 5, //mf_nozenypenalty
					 9, //mf_gvg
					11, //mf_notrade
					13, //mf_nowarp
					15, //mf_noicewall
					22, //mf_nogo
					33, //mf_noreturn
					34, //mf_nowarpto
					35, //mf_nightmaredrop
					38, //mf_nodrop
					41, //mf_novending
					44, //mf_noexppenalty
					14, //mf_partylock
					10; //mf_gvg_noparty
if($@debug) debugmes "Guild Mapflags loaded: "+getarraysize($@gvg_mf);
if($@same_gld) { 
// Guild Map Coordinates - used if $@same_gld is enabled
// Every Guild has two coordinates to spawn from:
// Guild 1: gld1_ > x1+y1 = North, x2+y2 = East
	setarray $@gvg_gld1_x1[0],9,9,12,8,18;
	setarray $@gvg_gld1_y1[0],50,50,51,49,50;
	setarray $@gvg_gld1_x2[0],50,50,50,50,49;
	setarray $@gvg_gld1_y2[0],7,7,13,8,23;
// Guild 2: gld2_ > x1+y1 = West, x2+y2 = South
	setarray $@gvg_gld2_x1[0],50,50,49,50,49;
	setarray $@gvg_gld2_y1[0],91,91,86,91,76;
	setarray $@gvg_gld2_x2[0],92,92,86,91,79;
	setarray $@gvg_gld2_y2[0],50,50,51,49,49;
	if($@debug) debugmes "Guild 1 Map Coordinates 1 loaded: X - "+getarraysize($@gvg_gld1_x1) +" & Y - "+getarraysize($@gvg_gld1_y1);
	if($@debug) debugmes "Guild 1 Map Coordinates 2 loaded: X - "+getarraysize($@gvg_gld1_x2) +" & Y - "+getarraysize($@gvg_gld1_y2);
	if($@debug) debugmes "Guild 2 Map Coordinates 1 loaded: X - "+getarraysize($@gvg_gld2_x1) +" & Y - "+getarraysize($@gvg_gld2_y1);
	if($@debug) debugmes "Guild 2 Map Coordinates 2 loaded: X - "+getarraysize($@gvg_gld2_x2) +" & Y - "+getarraysize($@gvg_gld2_y2);
}
// ======= Auto Mapflag Adding ===============//
for ( set .@m,0; .@m < getarraysize($@gvgr$); set .@m,.@m + 1 ) {
if($@debug) debugmes "Array Reading of GvG Maps complete";
if($@debug) debugmes "Mapflag set at "+$@gvgr$[.@m] + " initiate...";

// Use this if you disallows parties
//	for ( set .@mf,0; .@mf < getarraysize($@gvg_mf); set .@mf,.@mf + 1 ) {

// Use this if you allow parties
	for ( set .@mf,0; .@mf < (getarraysize($@gvg_mf) - 2); set .@mf,.@mf + 1 )	{
		setmapflag $@gvgr$[.@m],$@gvg_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@gvg_mf[.@mf] +" set at "+$@gvgr$[.@m] + " complete";
	}
}
if($@debug) debugmes "Guild Mapflag loading completed";
// ============ PvP Settings ============//
// ============= PvP Room Settings =======//
setarray $@room$[1],"Prontera","Alberta","Izlude","Payon","Morroc";
setarray $@muser[1],70,45,30,55,60;
if($@debug) debugmes "PvP Town Maps loaded: "+(getarraysize($@room$)-1);
// ======== Dispell PvP ======== //
setarray $@disppvp$[1],"pvp_y_1-1","pvp_y_1-4","pvp_y_1-2","pvp_y_1-3","pvp_y_1-5";
if($@debug) debugmes "PvP Dispell Maps loaded: "+(getarraysize($@disppvp$)-1);
// ======== Mapflags ======== //
// If you want to add more mapflags, 
// add them before the last two, which are for allowing party or not
setarray $@pvp_mf[0],0, //mf_nomemo
					 1, //mf_noteleport
					 2, //mf_nosave
					 3, //mf_nobranch
					 4, //mf_nopenalty
					 5, //mf_nozenypenalty
					11, //mf_notrade
					13, //mf_nowarp
					22, //mf_nogo
					33, //mf_noreturn
					34, //mf_nowarpto
					35, //mf_nightmaredrop
					38, //mf_nodrop
					41, //mf_novending
					44, //mf_noexppenalty
					14, //mf_partylock
					 7; //mf_pvp_noparty
if($@debug) debugmes "PvP Mapflags loaded: "+(getarraysize($@pvp_mf)-1);
// ======= Auto Mapflag Adding - Dispell PvP ===============//
for ( set .@m,1; .@m < getarraysize($@disppvp$); set .@m,.@m + 1 ) {
if($@debug) debugmes "Mapflag set at "+$@disppvp$[.@m] + " initiate...";
// Use this if you disallows parties
//	for ( set .@mf,0; .@mf < getarraysize($@pvp_mf); set .@mf,.@mf + 1 ) {

// Use this if you allow parties
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 2); set .@mf,.@mf + 1 ) {
		setmapflag $@disppvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@disppvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "PvP Dispell Mapflags loaded.";		
// Normal PvP
setarray $@npvp$[1],"pvp_y_2-1","pvp_y_2-4","pvp_y_2-2","pvp_y_2-3","pvp_y_2-5";
if($@debug) debugmes "PvP Normal Maps loaded: "+(getarraysize($@npvp$)-1);
// ======= Auto Mapflag Adding  - Normal PvP ===============//
for ( set .@m,1; .@m < getarraysize($@npvp$); set .@m,.@m + 1 ) {
if($@debug) debugmes "Mapflag set at "+$@npvp$[.@m] + " initiate...";
// Use this if you disallows parties
//	for ( set .@mf,0; .@mf < getarraysize($@pvp_mf); set .@mf,.@mf + 1 ) {

// Use this if you allow parties
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 2); set .@mf,.@mf + 1 ) {
		setmapflag $@npvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@npvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "PvP Normal Mapflags loaded.";			
// ================== Deathmatch Settings + Maps ================
setarray $@dmpvp$[1],"pvp_y_3-1","pvp_y_3-4","pvp_y_3-2","pvp_y_3-3","pvp_y_3-5";
if($@debug) debugmes "PvP DM Maps loaded: "+(getarraysize($@dmpvp$)-1);
// ======= Auto Mapflag Adding  - DM PvP ===============//
for ( set .@m,1; .@m < getarraysize($@dmpvp$); set .@m,.@m + 1 ) {
if($@debug) debugmes "Mapflag set at "+$@dmpvp$[.@m] + " initiate...";
// Use this if you disallows parties
//	for ( set .@mf,0; .@mf < getarraysize($@pvp_mf); set .@mf,.@mf + 1 ) {

// Use this if you allow parties
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 2); set .@mf,.@mf + 1 ) {
		setmapflag $@dmpvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@dmpvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "PvP DM Mapflags loaded.";		
// ================= Party PvP Settings =================
// ========= 2vs2 Party PvP ==========//
setarray $@pt2pvp$[1],"pvp_y_4-1","pvp_y_4-4","pvp_y_4-2","pvp_y_4-3","pvp_y_4-5";
if($@debug) debugmes "Party PvP 2vs2 Maps loaded: "+$@pt2pvp$[0];
// 3vs3 Party PvP
setarray $@pt3pvp$[1],"pvp_y_5-1","pvp_y_5-4","pvp_y_5-2","pvp_y_5-3","pvp_y_5-5";
if($@debug) debugmes "Party PvP 3vs3 Maps loaded: "+$@pt3pvp$[0];
// 4vs4 Party PvP
setarray $@pt4pvp$[1],"pvp_y_6-1","pvp_y_6-4","pvp_y_6-2","pvp_y_6-3","pvp_y_6-5";
if($@debug) debugmes "Party PvP 4vs4 Maps loaded: "+$@pt4pvp$[0];
// 5vs5 Party PvP
setarray $@pt5pvp$[1],"pvp_y_7-1","pvp_y_7-4","pvp_y_7-2","pvp_y_7-3","pvp_y_7-5";
if($@debug) debugmes "Party PvP 5vs5 Maps loaded: "+$@pt5pvp$[0];
// ======= Auto Mapflag Adding ===============//
// ============== Party 2vs2 =================//
if($@debug) debugmes "Array Reading of Party 2vs2 PvP Maps - configuring";
for ( set .@m,1; .@m < getarraysize($@pt2pvp$); set .@m,.@m + 1 ) {
	if($@debug) debugmes "Mapflag set at "+$@pt2pvp$[.@m] + " initiate...";
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 1); set .@mf,.@mf + 1 ) {
		setmapflag $@pt2pvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@pt2pvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "Party 2v2 PvP Mapflags loaded.";
// ============== Party 3vs3 =================//
if($@debug) debugmes "Array Reading of Party 3vs3 PvP Maps - configuring";
for ( set .@m,1; .@m < getarraysize($@pt3pvp$); set .@m,.@m + 1 ) {
	if($@debug) debugmes "Mapflag set at "+$@pt3pvp$[.@m] + " initiate...";
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 1); set .@mf,.@mf + 1 ) {
		setmapflag $@pt3pvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@pt3pvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "Party 3v3 PvP Mapflags loaded.";
// ============== Party 4vs4 =================//
	if($@debug) debugmes "Array Reading of Party 4vs4 PvP Maps - configuring";
for ( set .@m,1; .@m < getarraysize($@pt4pvp$); set .@m,.@m + 1 ) {
	if($@debug) debugmes "Mapflag set at "+$@pt4pvp$[.@m] + " initiate...";
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 1); set .@mf,.@mf + 1 ) {
		setmapflag $@pt4pvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@pt4pvp$[.@m] + " complete";
		sleep2 200;
	}
}
if($@debug) debugmes "Party 4v4 PvP Mapflags loaded.";
// ============== Party 5vs5 =================//
	if($@debug) debugmes "Array Reading of Party 5vs5 PvP Maps - configuring";
for ( set .@m,1; .@m < getarraysize($@pt5pvp$); set .@m,.@m + 1 ) {
	if($@debug) debugmes "Mapflag set at "+$@pt5pvp$[.@m] + " initiate...";
	for ( set .@mf,0; .@mf < (getarraysize($@pvp_mf) - 1); set .@mf,.@mf + 1 ) {
		setmapflag $@pt5pvp$[.@m],$@pvp_mf[.@mf];
		if($@debug) debugmes "Mapflag "+ $@pvp_mf[.@mf] +" set at "+$@pt5pvp$[.@m] + " complete";
	}
}
if($@debug) debugmes "Party 5v5 PvP Mapflags loaded.";
if($@debug) debugmes "Party PvP Mapflags loaded.";
// ============= Class Limitation ========//
// 0 = No 3rd Job Allowed
// 1 = Only 3rd Job Allowed
// 2 = All
// --------------------------------------
// Rooms: Prontera, Alberta, Izlude, Payon, Morroc
// * Dispell PvP Rooms
setarray $@class_disp[0],2,2,2,2,2;
// * Normal PvP Rooms
setarray $@class_n[1],2,2,2,2,2;
// * Deathmatch PvP Rooms
setarray $@class_dm[1],2,2,2,2,2;
// * Party PvP 2vs2 Rooms
setarray $@class_p2[1],2,2,2,2,2;
// * Party PvP 3vs3 Rooms
setarray $@class_p3[1],2,2,2,2,2;
// * Party PvP 4vs4 Rooms
setarray $@class_p4[1],2,2,2,2,2;
// * Party PvP 5vs5 Rooms
setarray $@class_p5[1],2,2,2,2,2;
//==============================
function writeStats;
if($@debug) debugmes "Write Stats Function loaded.";
function getBounty;
function writeBounty;
if($@debug) debugmes "Bounty Function loaded.";
function PVP_BTY_CMAP;
if($@debug) debugmes "Custom Bounty Map Function loaded.";
stopnpctimer;
set .time,getnpctimer(0);
if($@debug) debugmes "Loading Time: "+.time/1000+" seconds.";
freeloop(0);
set $@npc_load,0;
end;

OnPCKillEvent:
if(PVP_BTY_CMAP($@bty_cmap_type,strcharinfo(3)) == 1) end;
set .@v,killedrid;
mapannounce strcharinfo(3),strcharinfo(0) + " has killed "+rid2name(.@v)+"!!!!!",0;
set @killct,@killct + 1;
if($@debug) {
	debugmes "[Arena]: Showing debug messages";

	for ( set .@l,0; .@l < getarraysize($@sphree_kills); set .@l,.@l + 1) 
		debugmes "[Arena]: Sphree Kills Array - Index #"+.@l+": "+$@sphree_kills[.@l];
	for ( set .@l,0; .@l < getarraysize($@sphree_names$); set .@l,.@l + 1) 
		debugmes "[Arena]: Sphree Names Array - Index #"+.@l+": "+$@sphree_names$[.@l];
	for ( set .@l,0; .@l < getarraysize($@sphree_wav$); set .@l,.@l + 1) 
		debugmes "[Arena]: Sphree WAV Names Array - Index #"+.@l+": "+$@sphree_wav$[.@l];

	debugmes "[Arena]: Kill Counter Check and announces...";
	debugmes "[Arena]: Kills: "+@killct+", Arraysize $@sphree_kills: "+getarraysize($@sphree_kills)+", last sphree kills: "+$@sphree_kills[getarraysize($@sphree_kills)-1];
}
// Checking if the kill counter is below the last sphree number
if(@killct <= $@sphree_kills[getarraysize($@sphree_kills)-1]) {
	if($@debug) debugmes "[Arena]: Kills is less than last sphree kill.";
	// Looping through the array to check for the sphree kill
	for ( set .@k,0; .@k < getarraysize($@sphree_kills); set .@k,.@k + 1) {
		if($@debug) debugmes "[Arena]: Array Index:"+.@k+", Array Content: "+$@sphree_kills[.@k];
		if(@killct == $@sphree_kills[.@k]) {
			if($@debug) debugmes "[Arena]: "+@killct+", "+$@sphree_kills[.@k];
			mapannounce strcharinfo(3),strcharinfo(0) + " is now on "+$@sphree_names$[.@k]+"!!!!",0;
			soundeffectall $@sphree_wav$[.@k]+".wav",0,strcharinfo(3);
			setd("@"+$@sphree_rank$[.@k]),getd("@"+$@sphree_rank$[.@k]) + 1; // Ranking Variable
			break;
		}
	}
// Checking if the kill counter is above the last sphree number
} else if(@killct >= $@sphree_kills[getarraysize($@sphree_kills)-1]) {
	mapannounce strcharinfo(3),strcharinfo(0) + " is now on "+$@sphree_names$[getarraysize($@sphree_names$)-1]+"!!!!",0;
	soundeffectall $@sphree_wav$[getarraysize($@sphree_wav$)-1]+".wav",0,strcharinfo(3);
}
if($@debug) debugmes "[Arena]: Check Done";
set @prokill,@prokill + 1;

set @mkillct,@mkillct + 1;
deltimer strnpcinfo(0)+"::OnMultiSpan";
addtimer $@multi,strnpcinfo(0)+"::OnMultiSpan";
if(@mkillct >= 4) sleep2 500; // Slowing down multi-kill sound effects
if(@mkillct == 2) {
	mapannounce strcharinfo(3),strcharinfo(0) + " has made an Double Kill!!!!",0;
	soundeffectall "doublekill.wav",0,strcharinfo(3);
} else if(@mkillct == 3) {	
	mapannounce strcharinfo(3),strcharinfo(0) + " has made an Triple Kill!!!!",0;
	soundeffectall "triplekill.wav",0,strcharinfo(3);
} else if(@mkillct >= 4) {
	mapannounce strcharinfo(3),strcharinfo(0) + " has made an Ultra Kill!!!!",0;
	soundeffectall "ultrakill.wav",0,strcharinfo(3);
}
if($@bty_type && $@pvptable) {
	if(@killct >= $@bty_bon_start) {
		if(!@hunt) {
			announce "Bounty Hunter: Oh My God!!! Someone stop "+strcharinfo(0)+" please!!! I'll give you a bonus of "+($@bty_bonus*$@bty_bonus_m)+" " + ( ($@bty_type == 1)?"Zeny": ( ($@bty_type == 2)?"Cash Points":"Bounty Points") ) + " on "+ ( (Sex)?"his":"her") +" head!!",0;
			sleep2 2500;
			setarray .@pvp_pre$[0],"disp","dm","n","pt2","pt3","pt4","pt5";
			setarray .@pvp_n$[0],"Dispell","Deathmatch","Normal","Party 2vs2","Party 3vs3","Party 4vs4","Party 5vs5";
			for ( set .@l,0; .@l < getarraysize(.@pvp_pre$); set .@l,.@l + 1)
				for ( set .@m,1; .@m < getarraysize(getd("$@"+.@pvp_pre$[.@l]+"pvp$")); set .@m,.@m + 1)
					if(strcharinfo(3) == getd("$@"+.@pvp_pre$[.@l]+"pvp$["+.@m+"]")) {
						announce "Bounty Hunter: "+( (Sex)?"He":"She")+" is in the '"+$@room$[.@m]+" "+.@pvp_n$[.@l]+" Arena'!!!",0;
						break;
					}
			for ( set .@m,0; .@m < getarraysize($@gvgr$); set .@m,.@m + 1)
				if(strcharinfo(3) == $@gvgr$[.@m]) {
					announce "Bounty Hunter: "+( (Sex)?"He":"She")+" is in the Guild Room "+(.@m+1)+"!!!",0;
					break;
				}
				
			if($@bty_cmap) 
				for ( set .@m,0; .@m < getarraysize($@bty_cmaps$); set .@m,.@m + 1)
					if(compare(strcharinfo(3),$@bty_cmaps$[.@m]) == 1) {
						announce "Bounty Hunter: "+( (Sex)?"He":"She")+" is on the map '"+$@bty_cmaps$[.@m]+"'!!!",0;
						break;
					}

			set @hunt,1;
		}
		set @sphr,@killct + $@bty_bonus;
	}
	query_sql("SELECT `bounty` FROM `arena_master` WHERE `char_id` = '"+getcharid(0,rid2name(.@v))+"'",@bty);
	getBounty(@bty,.@v);
	writeBounty(@sphr,getcharid(0));
	writeStats(@killct,0);
}
if(@killct%5==0) message strcharinfo(0),"Defeated Players: "+@killct;
end;

OnPCDieEvent:
if(PVP_BTY_CMAP(1,strcharinfo(3)) == 1) end;
// ===== Should affects you only, if you warped to an player via @warpto/@goto or were @recall-ed
// ==== so you won't recieve a value for @map, which is required for the respawn in DM Maps
if(!@dm) {
	for( set @m,1; @m < getarraysize($@dmpvp$); set @m,@m + 1) 
		if(strcharinfo(3) == $@dmpvp$[@m]) {
			set @map,@m;
			set @dm,1;
			break;
		}
}
if($@pvptable) {
	writeStats(0,1);
	if($@bty_type) writeBounty(@sphr,getcharid(0));
}
set @sphr,0;
set @killct,0;

if($@same_gld && gvgc > 0) {
	if(getd("$@gvg_"+gvgc+"_gld_1") == getcharid(2)) {
		setd("$@gvg_"+gvgc+"_1_ppl"),getd("$@gvg_"+gvgc+"_1_ppl") - 1;
		if(getd("$@gvg_"+gvgc+"_1_ppl") < 1) setd("$@gvg_"+gvgc+"_gld_1"),0;
	} else if(getd("$@gvg_"+gvgc+"_gld_2") == getcharid(2)) {
		setd("$@gvg_"+gvgc+"_2_ppl"),getd("$@gvg_"+gvgc+"_2_ppl") - 1;
		if(getd("$@gvg_"+gvgc+"_2_ppl") < 1) setd("$@gvg_"+gvgc+"_gld_2"),0;
	}
	set gvgc,0;
}
if(@dm) {
	if($@debug) debugmes "KillerRid: "+killerrid+", KilledRid: "+killedrid;
	sleep2 1300;
	if(killerrid != 0 && killerrid != getcharid(3)) {
		warp strcharinfo(3),0,0;
		if(HP == 0) atcommand "@alive";
		percentheal 100,100; 
	} else warp "SavePoint",0,0;
}
set @hunt,0;
end;

OnMultiSpan:
set @mkillct,0;
end;

OnPCLogOutEvent:
if(PVP_BTY_CMAP($@bty_cmap_type,strcharinfo(3)) == 1) end;
if($@same_gld && gvgc > 0) {
	if(getd("$@gvg_"+gvgc+"_gld_1") == getcharid(2)) setd("$@gvg_"+gvgc+"_1_ppl"),getd("$@gvg_"+gvgc+"_1_ppl") - 1;
	else if(getd("$@gvg_"+gvgc+"_gld_2") == getcharid(2)) setd("$@gvg_"+gvgc+"_2_ppl"),getd("$@gvg_"+gvgc+"_2_ppl") - 1;
}
end;

OnNPCKillEvent:
if(!$@pvptable) end;
if(getmonsterinfo(killedrid,MOB_MVPEXP) > 0) { // Update to rAthena: getmonsterinfo supports now an check for MVP Exp
	if(query_sql("SELECT `char_id` FROM `arena_master` WHERE `char_id` = '"+getcharid(0)+"'",@charid) != 0)
		query_sql "UPDATE `arena_master` SET `mvp` = `mvp` + 1  WHERE `char_id` = '"+getcharid(0)+"'";
	else query_sql "INSERT INTO `arena_master` ( `char_id` , `mvp`) VALUES ( '"+getcharid(0)+"' , '1' )";
}
end;

	function	writeStats	{
	if(!$@pvptable) return; // To be save :O
	set @k,getarg(0); // Kills
	set @d,getarg(1); // Deaths
	if(query_sql("SELECT `kills` FROM `arena_master` WHERE `char_id` = '"+getcharid(0)+"'",@kills) != 0) {
		if(@k > query_sql("SELECT `hscore` FROM `arena_master` WHERE `char_id` = '"+getcharid(0)+"'",@hscore)) set @hscore,@k;
		// Adding General Kill/Death/Highscore Values
		query_sql "UPDATE `arena_master` SET `kills` = `kills` + "+@k+", `death` = `death` + "+@d+" , `hscore` = '"+@hscore+"' WHERE `char_id` = '"+getcharid(0)+"'";
		// Adding Sphree Ranking Values
		query_sql "UPDATE `arena_master` SET `killer` = `killer` + "+@killer+" , `domi` = `domi` + "+@domi+" , `mega` = `mega` + "+@mega+" , `unstopp` = `unstopp` + "+@unstopp+" , `wicked` = `wicked` + "+@wicked+" , `monster` = `monster` + "+@monster+" , `godlike` = `godlike` + "+@godlike+" , `holys` = `holys` + "+@holys+" WHERE `char_id` = '"+getcharid(0)+"'";
		// Reseting Ranking Variables
		for ( set .@l,0; .@l < getarraysize($@sphree_rank$); set .@l,.@l + 1)
			setd("@"+$@sphree_rank$[.@l]),0;
		
	} else query_sql "INSERT INTO `arena_master` ( `char_id` , `kills` , `death` , `hscore` , `killer` , `domi` , `mega` , `unstopp` , `wicked` , `monster` , `godlike` , `holys` ) VALUES ( '"+getcharid(0)+"' , '"+@k+"' , '"+@d+"' , '"+@k+"' , '"+@killer+"' , '"+@domi+"' , '"+@mega+"' , '"+@unstopp+"' , '"+@wicked+"' , '"+@monster+"' , '"+@godlike+"' , '"+@holys+"' )";
	return; 
	}
	
	function	getBounty	{
	// getarg(0) == Last Sphree of Victim
	// getarg(1) == RID of Victim
	if(!getarg(0)) return; // No Sphree = No Bounty ;I
	// Calculation:
	// Exchange the */+ with the symbol you want,
	// but note that it can be exploited if you change it too much,
	// like 8 (Sphree Index)*$@bty_price (Cash Points,5) = 40 CASHPOINTS
		
		// If is Bounty Bonus Multiplier active
		if($@bty_bonus_multi == 1) 
			set .@bty_b_gain,$@bty_bonus*$@bty_bonus_m;
		else
			set .@bty_b_gain,$@bty_bonus;
			
		if(getarg(0) >= $@bty_bon_start) 
			mapannounce strcharinfo(3),"Bounty Hunter: "+strcharinfo(0)+" has ended "+rid2name(getarg(1))+"'s Spree and was rewarded with additional "+.@bty_b_gain+" " + ( ($@bty_type == 1)?"Zeny": ( ($@bty_type == 2)?"Cash Points":"Bounty Points") ),0;
		
		switch($@bty_type) { // Bounty Reward Type
			case 1: // Zeny
			set Zeny,Zeny + ($@bty_price*getarg(0)) + .@bty_b_gain;
			dispbottom "Bounty Hunter: You have recieved "+($@bty_price*getarg(0)+.@bty_b_gain)+" Zeny for killing "+rid2name(getarg(1))+".";
			break;
			
			case 2: // Cash Points
			set #CASHPOINTS,#CASHPOINTS + ($@bty_price+getarg(0)) + .@bty_b_gain;
			dispbottom "Bounty Hunter: You have recieved "+($@bty_price+getarg(0)+.@bty_b_gain)+" Cash Points for killing "+rid2name(getarg(1))+".";
			dispbottom "You have now "+#CASHPOINTS+" Cash Points.";
			break;
			
			case 3: // Bounty Points
			query_sql "UPDATE `arena_master` SET `bounty_pts` = `bounty_pts` + "+($@bty_price+getarg(0)+.@bty_b_gain)+" WHERE `char_id` = '"+getcharid(0)+"'";
			dispbottom "Bounty Hunter: You have recieved "+($@bty_price+getarg(0)+.@bty_b_gain)+" Bounty Points for killing "+rid2name(getarg(1))+".";
			break;
		}
		writeBounty(0,getcharid(0,rid2name(getarg(1))));
		return;
	}
	
	function	writeBounty	{
	// getarg(0) == Kills
	// getarg(1) == Character ID
		query_sql "UPDATE `arena_master` SET `bounty` = '"+getarg(0)+"' WHERE `char_id` = '"+getarg(1)+"'";
		return;
	}
	
	function	PVP_BTY_CMAP	{
	// =====================================
	// getarg(0) - Flag Type > $@bty_cmap_type
	//        1 = Mapflags
	//        2 = Specific Maps
	// getarg(1) - strcharinfo(3) value
	// =====================================

	// Regular Maps in $@<.>pvp$ & $@gvgr$ array
		setarray .@pvp_pre$[0],"disp","dm","n","pt2","pt3","pt4","pt5";
		for ( set .@l,0; .@l < getarraysize(.@pvp_pre$); set .@l,.@l + 1)
			for ( set .@m,1; .@m < getarraysize(getd("$@"+.@pvp_pre$[.@l]+"pvp$")); set .@m,.@m + 1)
				if(getarg(1) != getd("$@"+.@pvp_pre$[.@l]+"pvp$["+.@m+"]"))
					set .@f,1;
				else 
					return 0;
				
		for ( set .@m,0; .@m < getarraysize($@gvgr$); set .@m,.@m + 1)
				if(getarg(1) != $@gvgr$[.@m])
					set .@f,1;
				else 
					return 0;
		
		// Mapflag
		if(getarg(0) == 1)
			if(getmapflag(getarg(1),mf_pvp) == 0 && getmapflag(getarg(1),mf_gvg) == 0) 
				set .@f,1;
			else 
				return 0;

		// Custom Maps
		else if(getarg(0) == 2) {
			// One Map
			if(getarraysize($@bty_cmaps$) == 1)
				if(compare(getarg(1),$@bty_cmaps$[0]) == 0)
					set .@f,1;
				else 
					return 0;

			// Array	
			else if(getarraysize($@bty_cmaps$) > 1)
				for ( set .@m,0; .@m < getarraysize($@bty_cmaps$); set .@m,.@m + 1)
					if(compare(getarg(1),$@bty_cmaps$[.@m]) == 0)
						set .@f,1;
					else 
						return 0;
		}
		return .@f;
	}
// =========== End of Function ==========
	
OnWhisperGlobal:
if(!getgmlevel()) end;
if(select("- ReLoad:- Nevermind") == 2) close;
goto OnInit;
end;
}