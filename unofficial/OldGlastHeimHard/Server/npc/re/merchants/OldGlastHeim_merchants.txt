//===== rAthena Script =======================================
//= Enchants NPCs.
//===== Description: =========================================
//= Temporal Boots enchants NPCs.
//===== Changelogs: ==========================================
//= 1.0 First Version merged from idathena.
//=     Credit exneval. [Capuche]
//= 1.1 Unofficial update for OGH Hard Mode [llchrisll]
//      Added Slotted Temporal Boots to be enchantable by using
//      Contaminated Magic
//      Added Unofficial(?) Reset feature to the Enchanter, disabled by default
//      Added support for the actual Card Slot in the Enchanter
//      Merged the White Knight and Khalitzburg Knight Card Trader into one NPC
//============================================================

// Merchant
//============================================================
glast_01,210,273,5	script	Hugin's butler#pa0829	1_F_04,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Buttler]";
	mes "Bring Temporal Crystal items for various Temporal boots items. Pick something you want.";
	next;
	switch( select( "Cancel", "Buy Temporal Boots (Need 1 Temporal Crystal)", "Upgrade Temporal Boots (Need 5 Temporal Crystals)", "Upgrade Modified Boots (Need 5 Time Crystals)" ) ) {
	case 1:
		mes "[Hugin's Buttler]";
		mes "Come back whenever you want.";
		close;
	case 2:
		if (countitem(6607) > 0) {
			mes "[Hugin's Buttler]";
			mes "Take these Temporal Boots. We can upgrade your Temporal Boots with 5 Temporal Crystals.";
			delitem 6607,1;// Temporal_Crystal
			getitem 2499,1;// Temporal_Boots
			close;
		}
		mes "[Hugin's Buttler]";
		mes "You need more Temporal Crystals. Come back when you get some more Temporal Crystals during your travel through Old Glast Heim.";
		close;
	case 3:
		setarray .@equip_type[0],
			22000,	// Temporal_STR_Boots
			22001,	// Temporal_INT_Boots
			22002,	// Temporal_AGI_Boots
			22003,	// Temporal_VIT_Boots
			22004,	// Temporal_DEX_Boots
			22005;	// Temporal_LUK_Boots
		break;
	case 4:
		setarray .@equip_type[0],
			22107,	// Modified_STR_Boots
			22108,	// Modified_INT_Boots
			22109,	// Modified_AGI_Boots
			22110,	// Modified_VIT_Boots
			22111,	// Modified_DEX_Boots
			22112;	// Modified_LUK_Boots
		break;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin's Buttler]";
		mes "Only the item you are wearing can be upgraded.";
		mes "Wear the ^0000FFTemporal Boots^000000 you want to upgrade, and then talk to me once again.";
		close;
	}
	if (getequipid(EQI_SHOES) != 2499) {
		mes "[Hugin's Buttler]";
		mes "You are not wearing the Temporal Boots. Talk to me once you're wearing them.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin's Buttler]";
		mes "Not enough Temporal Crystals. You need 5 Temporal Crystals to upgrade your item.";
		close;
	}
	mes "[Hugin's Buttler]";
	mes "You can trade ^0000FF1 Temporal Boots and 5 Temporal Crystals^000000 for 1 of the following items. ^FF0000Previous refine rate will not be retained with these new ones^000000.";
	next;
	.@s = select( "Cancel", "Str Boots", "Int Boots", "Agi Boots", "Vit Boots", "Dex Boots", "Luk Boots" ) - 2;
	if (.@s == -1) {
		mes "[Hugin's Buttler]";
		mes "Come back anytime you want.";
		close;
	}
	setarray .@type$[0], "Strength", "Intelligence", "Agility", "Vitality", "Dexterity", "Lucky";
	mes "[Hugin's Buttler]";
	mes "Are you sure about buying Temporal ^FF0000" + .@type$[.@s] + "^000000 Boots? There's no refunds.";
	next;
	if (select( "Think again", "Yes, I am sure." ) == 1) {
		mes "[Hugin's Buttler]";
		mes "Ok, come back again when you are sure.";
		close;
	}
	mes "[Hugin's Buttler]";
	mes "The boots have been upgraded as you want.";
	delequip EQI_SHOES;
	delitem 6607,5;// Temporal_Crystal
	getitem .@equip_type[.@s],1;
	close;
}

// Enchant and Socket NPC
//============================================================
glast_01,212,273,4	script	Hugin's magic master#pa0829	1_F_01,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Magic Master]";
	mes "So you came to enchant the Temporal Boots. Be aware that Boots with sockets will cost Contaminated Magic instead of Coagulated Spell and this upgrade is only possible for the boots you are wearing.";
	next;
	switch(select( "How does the enchanting work?", "Give effect to my Temporal Boots", ( (.enchant_reset)?"Remove effect on my Boots":"") )) {
		
		case 1:
		mes "[Hugin's Magic Master]";
		mes "You can keep obtain ^0000FFCoagulated Spell^000000 through your travel times.";
		next;
		mes "[Hugin's Magic Master]";
		mes "I can enchant your Temporal Boots items depands on the amount of that item. ^0000FFNot normal type boots^000000. Only possible for those 6 expanded version boots.";
		next;
		mes "[Hugin's Magic Master]";
		mes "I don't give out effect randomly. You can pick which effect you want for the item.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Of course it is for 4th slot only. You can have small effect at the start, and then the effect gets stronger.";
		next;
		mes "[Hugin's Magic Master]";
		mes "To get to the final option upgrade, it will require way more Coagulated Spell items. Don't forget that.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Once the 4th slot completes the best option, you get to have a random bonus effect to the 3rd slot. With.. certain payment~";
		next;
		mes "[Hugin's Magic Master]";
		mes "Also, you don't lose the item even if the enchant fails, we also guarantee keeping card and refine rate. But we can't initialize enchanting.";
		next;
		mes "[Hugin's Magic Master]";
		mes "Phew...";
		mes "Anyways, that's all for basic enchant information. Now you should see for yourself.";
		close;
	
		case 2:
		if (!getequipisequiped(EQI_SHOES)) {
			mes "[Hugin's Magic Master]";
			mes "Are you wearing the item?";
			close;
		}
		.@equip_id = getequipid(EQI_SHOES);

		switch(.@equip_id) {
			case 22000:	// Temporal_STR_Boots
			case 22001:	// Temporal_INT_Boots
			case 22002:	// Temporal_AGI_Boots
			case 22003:	// Temporal_VIT_Boots
			case 22004:	// Temporal_DEX_Boots
			case 22005:	// Temporal_LUK_Boots
			case 22107:	// Modified_STR_Boots
			case 22108:	// Modified_INT_Boots
			case 22109:	// Modified_AGI_Boots
			case 22110:	// Modified_VIT_Boots
			case 22111:	// Modified_DEX_Boots
			case 22112:	// Modified_LUK_Boots
			set .@t_type,1;
			break;
			
			case 22006: // Slotted Temporal_STR_Boots
			case 22007: // Slotted Temporal_INT_Boots
			case 22008: // Slotted Temporal_AGI_Boots
			case 22009: // Slotted Temporal_VIT_Boots
			case 22010: // Slotted Temporal_DEX_Boots
			case 22011: // Slotted Temporal_LUK_Boots
			case 22113:	// Slotted Modified_STR_Boots
			case 22114:	// Slotted Modified_INT_Boots
			case 22115:	// Slotted Modified_AGI_Boots
			case 22116:	// Slotted Modified_VIT_Boots
			case 22117:	// Slotted Modified_DEX_Boots
			case 22118:	// Slotted Modified_LUK_Boots
			set .@t_type,2;
			break;
		
			default:
			mes "[Hugin's Magic Master]";
			mes "This is not the right item for this enchant.";
			mes "Remember, only those 6 types of Temporal Boots are available for enchanting.";
			close;
		}
		.@equip_name$ = getequipname(EQI_SHOES);
		.@card0 = getequipcardid(EQI_ShOES,0); // Actual Card Slot
		.@card2 = getequipcardid(EQI_SHOES,2);
		.@card3 = getequipcardid(EQI_SHOES,3);
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);
		
		setarray .@enchant_1[0],4808,4832,4814,4741,4869,4752;// Fighting_Spirit4	Expert_Archer1	Spell2	Vitality2	DelayafterAttack1Lv	Luck3
		setarray .@enchant_2[0],4820,4833,4813,4742,4872,4753;// Fighting_Spirit5	Expert_Archer2	Spell3	Vitality3	DelayafterAttack2Lv	Luck4
		setarray .@enchant_3[0],4821,4834,4812,4861,4873,4754;// Fighting_Spirit6	Expert_Archer3	Spell4	MHP1		DelayafterAttack3Lv	Luck5
		setarray .@enchant_4[0],4822,4835,4826,4862,4881,4755;// Fighting_Spirit7	Expert_Archer4	Spell5	MHP2		DelayafterAttack4Lv	Luck6
		if(.@t_type == 1) // Use Coagulated Spell
			setarray .@enchant_cost[0],1,4,15,30,10;
		else if(.@t_type == 2) // Use Contaminated Magic
			setarray .@enchant_cost[0],1,4,15,30,10;

		if (.@card2 > 0) {
			mes "[Hugin's Magic master]";
			mes "These boots have already passed the enchanting limit. We can't enchant them any more.";
			close;
		}
		if (.@card3 == 0) {// 4th slot 1st try enchanting
			.@cost = .@enchant_cost[0];
			mes "[Hugin's Magic Master]";
			mes "Want to enchant ^0000FF" + .@equip_name$ + "^000000?";
			if(.@t_type == 1)
				mes "For the 1st enchanting, you need ^0000FF" + .@cost + "^000000 Coagulated Spell items.";
			else if(.@t_type == 2)
				mes "For the 1st enchanting, you need ^0000FF" + .@cost + "^000000 Contaminated Magic items.";
			next;
			.@s = select( "Quit", "Fighting Spirit", "Archery", "Spell", "Vitality", "Attack Speed", "Lucky" ) - 2;
			if (.@s == -1) {
				mes "[Hugin's Magic Master]";
				mes "Ok, come back when you are ready.";
				close;
			}
			.@card3 = .@enchant_1[.@s];
			.@string$ = "enchant number ^6300001^000000.";
		}
			else {
			for ( .@enchant_num = 1; .@enchant_num < 5; .@enchant_num++ ) {
				for ( .@enchant_type = 0; .@enchant_type < 6 && .@card3 != getd( ".@enchant_" + .@enchant_num + "[" + .@enchant_type + "]" ); .@enchant_type++ )
					continue;
				if (.@enchant_type < 6)
					break;
			}
			if (.@enchant_num == 5) {
				mes "[Hugin's Magic Master]";
				mes "Something wrong happened.";
				close;
			}
			.@cost = .@enchant_cost[.@enchant_num];
				mes "[Hugin's Magic Master]";
			if (.@enchant_num == 4) {
				.@card2 = callfunc("F_Rand",4875,4876,4877,4878,4879,4880);// Bear's_Power, Runaway_Magic, Speed_Of_Light, Muscle_Fool, Hawkeye, Lucky_Day
				.@string$ = "^990000Bonus effect ^000000 upgrade.";
				if(.@t_type == 1)
					mes "4th slot is already upgraded to the final option. Would you like a random bonus effect for the 3rd slot? You need ^0000ff" + .@cost + "^000000 Coagulated Spell.";
				else if(.@t_type == 2)
					mes "4th slot is already upgraded to the final option. Would you like a random bonus effect for the 3rd slot? You need ^0000ff" + .@cost + "^000000 Contaminated Magic.";
					
			}
			else {
				.@number = .@enchant_num + 1;
				.@card3 = getd( ".@enchant_" + (.@enchant_num+1) + "[" + .@enchant_type + "]" );
				.@string$ = "enchant number ^630000" + .@number + "^000000.";
				if(.@t_type == 1)
					mes "Enchanting ^0000FF" + .@equip_name$ + "^000000's 4th slot as ^0000FF" + .@number + "^000000 level effect. Requires ^0000FF" + .@cost + "^000000 Coagulated Spell.";
				else if(.@t_type == 2)
					mes "Enchanting ^0000FF" + .@equip_name$ + "^000000's 4th slot as ^0000FF" + .@number + "^000000 level effect. Requires ^0000FF" + .@cost + "^000000 Contaminated Magic.";
			}
			next;
			if (select("Quit","Effect Upgrade!") == 1) {
				mes "[Hugin's Magic Master]";
				mes "Ok, come back when you are ready.";
				close;
			}
		}
		if(.@t_type == 1 && countitem(6608) < .@cost) {
			mes "[Hugin's Magic Master]";
			mes "Hmm, you are missing " + (.@cost - countitem(6608)) + " Coagulated Spell. Go get more, and then we can talk about more enchants.";
			close;
		
		} else if(.@t_type == 2 && countitem(6755) < .@cost) {
			mes "[Hugin's Magic Master]";
			mes "Hmm, you are missing " + (.@cost - countitem(6755)) + " Contaminated Magic. Go get more, and then we can talk about more enchants.";
			close;
		}
		specialeffect2 EF_REPAIRWEAPON;
		if(.@t_type == 1)
			delitem 6608,.@cost;// Coagulated_Spell
		else if(.@t_type == 2)
			delitem 6755,.@cost;// Contaminated_Magic
		delequip EQI_SHOES;
		mes "[Hugin's Magic Master]";
		mes "Trying for " + .@string$;
		getitem2 .@equip_id,1,1,.@equip_refine,0,.@card0,0,.@card2,.@card3;
		close;
		
		case 3:
		if (!getequipisequiped(EQI_SHOES)) {
			mes "[Hugin's Magic Master]";
			mes "Are you wearing the item?";
			close;
		}
		if(getequipcardid(EQI_SHOES,3) == 0) {
			mes "[Hugin's Magic Master]";
			mes "There is no effect on your boots!";
			close;
		}
		if(.reset_boots || .enchant_reset == 2) {
			mes "[Hugin's Magic Master]";
			mes "I can remove the effect on your boots:";
			
			if(.reset_boots) 
				mes " > if they are "+( (.reset_boots == 1)?"unslotted":"slotted");

			if(.enchant_reset == 2) {
				if(!.reset_restrict)
					mes " > if they only have one effect";
				
				else if(.reset_restrict && .@equip_refine < .reset_refine)
					mes " > if they are refined to at least +"+.reset_refine+".";
			}
			mes " ";
		}
		mes "Every card inserted will be "+ ( (!.reset_card)?"ignored":"removed")+".";
		if(.reset_price) {
			mes "Also I need the following items to start:";
			for ( .@p = 0; .@p < getarraysize(.reset_cost); .@p += 2)
				mes " > "+ .reset_cost[.@p+1]+"x "+getitemname(.reset_cost[.@p]);
		}
		next;
		mes "[Hugin's Magic Master]";
		if(.reset_chance < 100)
			mes "There is a chance to fail the process, but I will try to keep the boots intact!";
		mes "Do you really want to remove the effects on your boots?";
		if(select("- Yes:- No") - 1) close;
		next;
		
		.@equip_id = getequipid(EQI_SHOES);

		switch(.@equip_id) {
			case 22000:	// Temporal_STR_Boots
			case 22001:	// Temporal_INT_Boots
			case 22002:	// Temporal_AGI_Boots
			case 22003:	// Temporal_VIT_Boots
			case 22004:	// Temporal_DEX_Boots
			case 22005:	// Temporal_LUK_Boots
			case 22107:	// Modified_STR_Boots
			case 22108:	// Modified_INT_Boots
			case 22109:	// Modified_AGI_Boots
			case 22110:	// Modified_VIT_Boots
			case 22111:	// Modified_DEX_Boots
			case 22112:	// Modified_LUK_Boots
			set .@t_type,1;
			break;

			case 22006: // Slotted Temporal_STR_Boots
			case 22007: // Slotted Temporal_INT_Boots
			case 22008: // Slotted Temporal_AGI_Boots
			case 22009: // Slotted Temporal_VIT_Boots
			case 22010: // Slotted Temporal_DEX_Boots
			case 22011: // Slotted Temporal_LUK_Boots
			case 22113:	// Slotted Modified_STR_Boots
			case 22114:	// Slotted Modified_INT_Boots
			case 22115:	// Slotted Modified_AGI_Boots
			case 22116:	// Slotted Modified_VIT_Boots
			case 22117:	// Slotted Modified_DEX_Boots
			case 22118:	// Slotted Modified_LUK_Boots
			set .@t_type,2;
			break;

			default:
			mes "[Hugin's Magic Master]";
			mes "This is not the right item for this enchant.";
			mes "Remember, only those 6 types of Temporal Boots are available for enchanting.";
			close;
		}
		.@card0 = getequipcardid(EQI_ShOES,0); // Actual Card Slot
		.@card2 = getequipcardid(EQI_SHOES,2);
		.@card3 = getequipcardid(EQI_SHOES,3);
		.@equip_refine = getequiprefinerycnt(EQI_SHOES);
		
		mes "[Hugin's Magic Master]";
		if(.reset_boots && .@t_type != .reset_boots) {
			mes "I'm sorry, but I can only reset "+ ( (.reset_boots == 1)?"Unslotted":"Slotted")+" Temporal Boots.";
			close;
		}
		if(.enchant_reset == 2) {
			if(!.reset_restrict && .@card2) {
				mes "I'm sorry, but I can't reset the effects on your boots, because you have already 2 of them.";
				close;
				
			} else if(.reset_restrict && .@equip_refine < .reset_refine) {
				mes "I'm sorry, but you're boots have to be refined to at least +"+.reset_refine+".";
				mes "Otherwise the boots won't be strong enough to withstand the process.";
				close;
			}
		}
		if(.reset_price) {
			for ( .@p = 0; .@p < getarraysize(.reset_cost); .@p += 2)
				if(countitem(.reset_cost[.@p]) < .reset_cost[.@p+1]) {
					mes "You are missing "+(.reset_cost[.@p+1] - countitem(.reset_cost[.@p]))+"x "+getitemname(.reset_cost[.@p])+"...";
					.@f++;
				}
			if(.@f) close;

			for ( .@p = 0; .@p < getarraysize(.reset_cost); .@p += 2)
				delitem .reset_cost[.@p],.reset_cost[.@p+1];
		}
		.@r = rand(1,100);
		if( .@r >= (100 - .reset_chance)) {	// Success
			delequip EQI_SHOES;
			if(.reset_card) {
				getitem .@card0,1;
				.@card0 = 0;
			}
			getitem2 .@equip_id,1,1,.@equip_refine,0,.@card0,0,0,0;
			mes "Removal complete!";
		} else
			mes "I'm sorry, but the process has failed. Please try again.";
	}
end;

OnInit:
// === Reset Feature ===
// 0 = Disabled (Official)
// 1 = Normal Reset
// 2 = Restricted Reset
.enchant_reset = 0;
if(.enchant_reset) {
	// === Type of Boots resetable ===
	// 0 = All (default)
	// 1 = Unslotted only
	// 2 = Slotted only
	.reset_boots = 0;
	
	// === Card Slot behaviour on reset ===
	// 0 = Card will not be removed (default)
	// 1 = Card will be removed
	.reset_card = 0;
	
	// === Restricted Reset ===
	if(.enchant_reset == 2) {
		// 0 = Only 1 Enchantment
		// 1 = Required Refine
		.reset_restrict = 0;
		
		if(.reset_restrict)
			// === Required Refine for Reset === 
			.reset_refine = 7;
	}
	// === Chance of Reset == 
	// > Define here the chance for the reset
	// 50% = Default
	.reset_chance = 50;

	// === Reset Price ===
	// 0 = Disabled
	// 1 = Enabled
	.reset_price = 1;
	if(.reset_price)
		// === Reset Price List === 
		// Note: You can put as many as you want
		setarray .reset_cost[0],
		// 			Format: <Item ID>,<Cost>
								6607,5,	 // Temporal Crystal
								6755,10, // Coagulated Spell 
								6608,10; // Contaminated Magic
}
end;
}

glast_01,210,270,0	script	Hugin's Craftsman#pa0829	4_F_JOB_BLACKSMITH,{
	disable_items;
	if (MaxWeight - Weight < 1000) {
		mes "Your bag is too heavy. Reduce some weight and come back.";
		close;
	}
	mes "[Hugin's Craftsman]";
	mes "That Magic Master always says that enchanting Temporal Boots is the best. But, I think quite different.";
	next;
	mes "[Hugin's Craftsman]";
	mes "I think putting a slot into the item would be the best. Even with some risk of failing.";
	next;
	mes "[Hugin's Craftsman]";
	mes "Bring me ^0000FF5 Temporal Crystals^000000, and I will try to put a slot into your advanced Temporal Boots. But, it's possible to ^FF0000fail and break the item^000000. Is that ok?";
	next;
	if (select( "Cancel", "Take the risk and try to add a slot" ) == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, come back anytime you want.";
		close;
	}
	if (!getequipisequiped(EQI_SHOES)) {
		mes "[Hugin's Craftsman]";
		mes "Are you sure you are wearing the item?";
		close;
	}
	.@equip_id = getequipid(EQI_SHOES);
	switch(.@equip_id) {
	case 22000:	callsub( S_Slot,22006 );	// Temporal_STR_Boots
	case 22001:	callsub( S_Slot,22009 );	// Temporal_INT_Boots
	case 22002:	callsub( S_Slot,22010 );	// Temporal_AGI_Boots
	case 22003:	callsub( S_Slot,22007 );	// Temporal_VIT_Boots
	case 22004:	callsub( S_Slot,22008 );	// Temporal_DEX_Boots
	case 22005:	callsub( S_Slot,22011 );	// Temporal_LUK_Boots

	case 22107:	callsub( S_Slot,22113 );	// Modified_STR_Boots
	case 22108:	callsub( S_Slot,22114 );	// Modified_INT_Boots
	case 22109:	callsub( S_Slot,22115 );	// Modified_AGI_Boots
	case 22110:	callsub( S_Slot,22116 );	// Modified_VIT_Boots
	case 22111:	callsub( S_Slot,22117 );	// Modified_DEX_Boots
	case 22112:	callsub( S_Slot,22118 );	// Modified_LUK_Boots
	default:
		mes "[Hugin's Craftsman]";
		mes "This is not it! We need ^0000FFAdvanced Temporal Boots^000000 items! The item ^0000FFwith a stat effect and without a slot^000000!";
		close;
	}

S_Slot:
	mes "[Hugin's Craftsman]";
	mes "And ^FF0000all enchant or refine rates will be lost if you fail this.^000000";
	mes "Still want to risk it?";
	next;
	if (select( "Cancel", "I am ok with it!" ) == 1) {
		mes "[Hugin's Craftsman]";
		mes "Ok, come back anytime you want.";
		close;
	}
	if (countitem(6607) < 5) {
		mes "[Hugin's Craftsman]";
		mes "You must bring ^0000FF5 Temporal Crystal items^000000. Nothing is free you know...";
		close;
	}
	delitem 6607,5;// Temporal_Crystal
	delequip EQI_SHOES;
	if (rand(1,100) < 50) {
		mes "[Hugin's Craftsman]";
		mes "Arrggg, we failed. Better luck next time.";
		specialeffect2 EF_PHARMACY_FAIL;
		close;
	}
	mes "[Hugin's Craftsman]";
	mes "Successful! Here you go.";
	specialeffect2 EF_MAXPOWER;
	getitem getarg(0),1;
	close;
}

// NPC for White Knight Card and Khalitzburg Knight Card
glast_01,196,273,5	script	Portrait Collector#glast	4_M_OILMAN,{

disable_items;
mes .n$;
mes "I have been collecting these portraits for a year now...";
mes "It is time to show my collection to the world.";
next;
mes .n$;
mes "I willing to sell them for Coagulated Spells or Contaminated Magics.";
next;
switch(select("Ask about pricing:Purchase White Knight Card:Purchase Khalitzburg Knight Card:End Conversation")) {
	case 1:
	mes .n$;
	mes "The price of a White Knight Card is 3,000 Coagulated Spells or 700 Contaminated Magics.";
	next;
	mes .n$;
	mes "The price of a Khalitzburg Knight Card is 5,000 Coagulated Spells or 1,000 Contaminated Magics.";
	close;
	
	case 2:
	mes .n$;
	mes "How would you like to pay for your <ITEM>"+getitemname(4608)+"<INFO>4608</INFO></ITEM>?";
	next;
	switch(select("3,000 "+getitemname(6608)+"s:700 "+getitemname(6755)+"s:End Conversation")) {
		case 1:
		mes .n$;
		if(countitem(6608) < 3000) {
			mes "You don't have enough "+getitemname(6608)+"s, please check your Inventory.";
			close;
		}
		mes "Trade completed.";
		delitem 6608,3000;
		getitem 4608,1;
		break;
		
		case 2:
		mes .n$;
		if(countitem(6755) < 700) {
			mes "You don't have enough "+getitemname(6755)+"s, please check your Inventory.";
			close;
		}
		mes "Trade completed.";
		delitem 6755,700;
		getitem 4608,1;
		break;
		
		case 3:
		mes .n$;
		mes "Come back if you decide you do want to purchase one.";
		break;
	}
	break;
	
	case 3:
	mes .n$;
	mes "How would you like to pay for your <ITEM>"+getitemname(4609)+"<INFO>4609</INFO></ITEM>?";
	next;
	switch(select("5,000 "+getitemname(6608)+"s:1000 "+getitemname(6755)+"s:End Conversation")) {
		case 1:
		mes .n$;
		if(countitem(6608) < 5000) {
			mes "You don't have enough "+getitemname(6608)+"s, please check your Inventory.";
			close;
		}
		mes "Trade completed.";
		delitem 6608,5000;
		getitem 4609,1;
		break;
		
		case 2:
		mes .n$;
		if(countitem(6755) < 1000) {
			mes "You don't have enough "+getitemname(6755)+"s, please check your Inventory.";
			close;
		}
		mes "Trade completed.";
		delitem 6755,1000;
		getitem 4609,1;
		break;
		
		case 3:
		mes .n$;
		mes "Come back if you decide you do want to purchase one.";
		break;
	}
	break;
	
	case 4:
	mes .n$;
	mes "Come back if you decide you do want to purchase one.";
	break;
}
close;
	
OnInit:
.n$ = "["+strnpcinfo(0)+"]";
end;
}